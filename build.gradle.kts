// Please do not add things here unless really necesary in order to stay compatible with
// project isolation when it comes out (https://gradle.github.io/configuration-cache/#project_isolation)

plugins {
   id("com.autonomousapps.dependency-analysis")
}

dependencyAnalysis {
   issues {
      all {
         onAny {
            // Rebugger and WhatTheStack are always included as a convenience, so it can immediately be used. It's debug only anyway
            exclude("io.github.theapache64:rebugger")
            exclude("com.github.matejdro:WhatTheStack")

            // Included by kotlin automatically, we can't affect it
            exclude("org.jetbrains.kotlin:kotlin-stdlib")

            // False positives, maybe generated by the MoshiX?
            exclude("com.squareup.moshi:moshi")

            // Fail build if there are any violations
            severity("fail")
         }

         onUnusedDependencies {
            // Standard test dependencies, included in all modules by default for huge convenience boost
            exclude("org.junit.jupiter:junit-jupiter-api")
            exclude("io.kotest:kotest-assertions-core")
            exclude("org.jetbrains.kotlinx:kotlinx-coroutines-test")
            exclude("app.cash.turbine:turbine")
            exclude("si.inova.kotlinova:navigation-test")

            // Standard sqldelight dependencies, always included for convenience
            exclude("app.cash.sqldelight:async-extensions")

            // Standard compose dependencies, always included for convenience
            exclude("androidx.compose.ui:ui-graphics")
            exclude("androidx.compose.ui:ui-test-junit4")
            exclude("androidx.compose.material3:material3")
            exclude("androidx.compose.material:material")
            exclude("androidx.lifecycle:lifecycle-runtime-compose")
            exclude("si.inova.kotlinova:compose")
         }

         onIncorrectConfiguration {
            // Showkase is only used by the generated code, by app module that also needs to explicitly include showkase
            exclude("com.airbnb.android:showkase")

            // Dagger runtime annotations are useless without explicit dagger dependency
            exclude("com.google.dagger:dagger")

            // Navigation should be included as-needed to reduce already huge amount of modules depending on it
            exclude(":common-navigation")
         }

         onUsedTransitiveDependencies {
            // This is fine, included with dagger
            exclude("javax.inject:javax.inject")

            // This is fine, included with anvil
            exclude("com.squareup.anvil:annotations")

            // This is fine, included with sqldelight
            exclude("androidx.sqlite:sqlite")

            // This is fine, included with paparazzi
            exclude("com.android.tools.layoutlib:layoutlib-api")
            exclude("app.cash.paparazzi:layoutlib-native-jdk11")

            // This is fine, included with parcelize plugin
            exclude("org.jetbrains.kotlin:kotlin-parcelize-runtime")

            // This is fine, included with detekt API
            exclude("org.jetbrains.kotlin:kotlin-compiler-embeddable")

            // This is fine, included with kotlinova navigation
            exclude("com.github.Zhuinden:simple-stack")
         }
      }

      project(":app-screenshot-tests") {
         onIncorrectConfiguration {
            // screenshot tests need to include app as implementation, otherwise resources do not work properly
            exclude(":app")
         }
      }
   }

   structure {
      ignoreKtx(true)

      bundle("coil") {
         // We only ever want coil-compose, so coil is considered as a group
         includeGroup("io.coil-kt")
      }

      bundle("compose") {
         // Compose libraries are blanket-included to for convenience. It shouldn't cause a big issue
         includeGroup("androidx.compose.animation")
         includeGroup("androidx.compose.foundation")
         includeGroup("androidx.compose.material")
         includeGroup("androidx.compose.material3")
         includeGroup("androidx.compose.runtime")
         includeGroup("androidx.compose.ui")
      }

      // Library Groups:

      bundle("androidxActivity") {
         includeGroup("androidx.activity")
      }

      bundle("androidxCore") {
         includeGroup("androidx.core")
      }

      bundle("androidxLifecycle") {
         includeGroup("androidx.lifecycle")
      }

      bundle("anvil") {
         includeGroup("com.squareup.anvil")
      }

      bundle("datastore") {
         includeGroup("androidx.datastore")
      }

      bundle("kotest") {
         includeGroup("io.kotest")
      }

      bundle("showkase") {
         includeGroup("com.airbnb.android")
      }

      bundle("sqlDelight") {
         includeGroup("app.cash.sqldelight")
      }
   }
}
